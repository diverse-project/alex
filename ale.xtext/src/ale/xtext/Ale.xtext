grammar ale.xtext.Ale with org.eclipse.xtext.xbase.Xbase

generate ale "http://www.xtext.ale/Ale"

Root returns Root:
	'behavior' name=ID ';'
	importSection=XImportSection?
	importEcore=ImportEcore
	importsAle+=ImportAle*
	classes+=AleClass*
;

ImportAle:
	{ImportAle} 'import' 'ale' ref=[Root] ';'
;

ImportEcore:
	{ImportEcore} 'import' 'ecore' ref=STRING ';'
;


AleClass:
	'open' 'class' name=ValidID '{'
		methods+=Method*
	'}'
;

Method:
	AbstractMethod | ConcreteMethod
;
	
ConcreteMethod:
	DefMethod | OverrideMethod
;

AbstractMethod:
	'abstract' 'def' type=JvmTypeReference name=ValidID
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
	';'
;

DefMethod:
	'def' type=JvmTypeReference name=ValidID
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
	block=XBlockExpression
;

OverrideMethod:
	'override' type=JvmTypeReference name=ValidID
	'(' (params+=FullJvmFormalParameter (',' params+=FullJvmFormalParameter)*)? ')'
	block=XBlockExpression
;

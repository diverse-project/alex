behavior modularactivitydiagram

import activitydiagram.ControlFlow
import activitydiagram.Token
import java.util.List

import ecore "platform:/resource/org.modelexecution.operationalsemantics.adruntime/model/adruntime.genmodel"

open class Expression {
	def void execute() {}
}

open class ActivityEdge_Aspect {
	def Boolean isControlFlow() {
		return false;
	}
	
	def ControlFlow asControlFlow() {
		return null;
	}
	
	def void sendOffer(List<Token> tokens) {
		Offer offer = new Offer();
		for(Token token in tokens) {
			offer.getOfferedTokens().add(token);
		}

		obj.getOffers().add(offer);
	}

	def Sequence(Token) takeOfferedTokens() {
		Sequence(Token) tokens = newSequence(Token);

		for(Offer o in obj.getOffers()) {
			tokens.addAll(o.getOfferedTokens());
		}
		
		obj.getOffers().clear();
		
		return tokens;
	}
	
	def Boolean hasOffer() {
		for(Offer o in obj.getOffers()) {
			if($[o].hasTokens()) { 
				return true;
			}
		}
		return false;
	}
}

open class Offer {
	
	def Boolean hasTokens() {
		$[obj].removeWithdrawnTokens();
		return obj.getOfferedTokens().size() > 0;
	}
	
	def void removeWithdrawnTokens() {
		Sequence(Token) tokensToBeRemoved = newSequence(Token);
		for(Token token in obj.getOfferedTokens()) {
			if($[token].isWithdrawn()) { 
				tokensToBeRemoved.add(token);
			}
		} 
		obj.getOfferedTokens().removeAll(tokensToBeRemoved);
	}
}

open class ActivityNode_Aspect { // extends NamdeElement
	def Boolean isInitialNode() {
		return false;
	}
	
	def InitialNode asInitialNode() {
		return null;
	}
	
	def void run() {
		obj.setRunning(true);
	}
	
	def Boolean isRunning() {
		return obj.isRunning();
	}
	
	def void terminate() {
		obj.setRunning(false);
	}
	
	def Boolean isReady() {
		return obj.isRunning();
	}
	
	def void sendOffers(Sequence(Token) tokens) {
		for(ActivityEdge edge in obj.getOutgoing()) {
			$[edge].sendOffer(tokens); 
		}
	}
	
	def Sequence(Token) takeOfferedTokens() {
		Sequence(Token) allTokens = newSequence(Token);
		for(ActivityEdge edge in obj.getIncoming()) {
			Sequence(Token) tokens = $[edge].takeOfferedTokens(); 
			for(Token token in tokens) {
				$[token].withdraw(); 
			}
			
			allTokens.addAll(tokens);
		}
		
		return allTokens;
	}
	
	def void addTokens(Sequence(Token) tokens) {
		for(Token token in tokens) {
			Token transferredToken = $[token].transfer(obj);
			obj.getHeldTokens().add(transferredToken);
		}
	}
	
	def Boolean hasOffers() {
		Boolean hasOffer = true;
		for(ActivityEdge edge in obj.getIncoming()) {
			if(not $[edge].hasOffer()) {
				hasOffer = false;
			}
		}
		return hasOffer;
	}
	
	def void removeToken(Token token) {
		obj.getHeldTokens().remove(token);
	}
	
	def void fire(Sequence(Token) tokens) {}
}

open class Token {
	
	def void withdraw() {
		if(not $[obj].isWithdrawn()) {
			$[obj.getHolder()].removeToken(obj);
			obj.setHolder(null);	
		}
	}
	
	def Boolean isWithdrawn() {
		return obj.getHolder() == null;
	}
	
	def Token transfer(ActivityNode holder) {
		if(obj.getHolder() != null) {
			$[obj].withdraw();
		}
		
		obj.setHolder(holder);
		
		return obj;
	}
}

open class ControlToken extends Token {
	
}

open class ForkedToken extends Token {
	
	def void withdraw() {
		if(not $[obj.getBaseToken()].isWithdrawn()) {
			$[obj.getBaseToken()].withdraw();
		}
		
		if(obj.getRemainingOffersCount() > 0) {
			obj.setRemainingOffersCount(obj.getRemainingOffersCount() - 1);
		}
		
		if(obj.getRemainingOffersCount() == 0) {
			super.withdraw();
		}
	}	
}


open class Activity_Aspect {
	
	def void initialize(Sequence(InputValue) inputValues) {
		for(Variable v in obj.getLocals()) {
			v.setCurrentValue(v.getInitialValue());
		}
		if(inputValues != null) {
			for(InputValue v in inputValues) {
				v.getVariable().setCurrentValue(v.getValue());
			}
		}
	}
	
	def void main(Sequence(InputValue) inputValues) {
		$[obj].initialize(inputValues);
		$[obj].initializeTrace();
		$[obj].run();
	}
	
	def void run() {
		$[obj].runNodes();
		$[obj].fireInitialNode();
		Sequence(ActivityNode) enabledNodes = $[obj].getEnabledNodes();
		while(enabledNodes.size() > 0) {
			ActivityNode nextNode = $[obj].selectNextNode(enabledNodes);
			$[obj].fireNode(nextNode);
			enabledNodes = $[obj].getEnabledNodes();
		}
	}
	
	def ActivityNode selectNextNode(Sequence(ActivityNode) activityNodes) {
		return activityNodes.get(0);
	}
	
	def Sequence(ActivityNode) getEnabledNodes() {
		Sequence(ActivityNode) enabledNodes = newSequence(ActivityNode);
		for(ActivityNode node in obj.getNodes()) {
			if($[node].isReady()) { 
				enabledNodes.add(node);
			}
		}
		
		return enabledNodes;
		
	}
	
	def void fireInitialNode() {
		InitialNode initialNode = $[obj].getInitialNode();
		$[obj].fireNode(initialNode);
	}
	
	def void fireNode(ActivityNode node) {
		Sequence(Token) tokens = $[node].takeOfferedTokens(); 
		$[node].fire(tokens); 
		obj.getTrace().getExecutedNodes().add(node); 
	}
	
	def InitialNode getInitialNode() {
		for(ActivityNode node in obj.getNodes()) {
			if($[node].isInitialNode()) {
				return $[node].asInitialNode();
			}
		}
		
		return null;
	}
	
	def void runNodes() {
		for(ActivityNode node in obj.getNodes()) {
			$[node].run(); 
		}
	}
	
	def void initializeTrace() {
		Trace trace = new Trace();
		obj.setTrace(trace);
	}
	
	def void terminate() {
		for(ActivityNode node in obj.getNodes()) {
			$[node].terminate();
		}
	}
}

open class Action extends ExecutableNode {}

open class Action_Aspect extends ExecutableNode_Aspect {
	def void fire(Sequence(Token) tokens) {
		$[obj].doAction();
		$[obj].sendOffers();
	}
	
	def void doAction() {
		// abstract
	}
	
	
	def void sendOffers() {
		if(obj.getOutgoing().size() > 0) {
			Sequence(Token) tokens = newSequence(Token);
			tokens.add(new ControlToken());
			$[obj].addTokens(tokens);
			$[obj.getOutgoing().get(0)].sendOffer(tokens);
		}
	}
	
	def Boolean isReady() {
		return super.isReady() and $[obj].hasOffers();
	}
}

open class FinalNode extends ControlNode {}

open class FinalNode_Aspect extends ControlNode_Aspect {
	
}
open class ActivityFinalNode extends FinalNode {}

open class ActivityFinalNode_Aspect extends FinalNode_Aspect {
	def void fire(Sequence(Token) tokens) {
		$[obj.getActivity()].terminate(); 
	}
}

open class ControlNode extends ActivityNode {}

open class ControlNode_Aspect extends ActivityNode_Aspect {
	def void fire(Sequence(Token) tokens) {
		$[obj].addTokens(tokens);
		$[obj].sendOffers(tokens);
	}
	
	def Boolean isReady() {
		return super.isReady() and $[obj].hasOffers();
	}
}
open class DecisionNode extends ControlNode {}
open class DecisionNode_Aspect extends ControlNode_Aspect {
	def void fire(Sequence(Token) tokens) {
		ActivityEdge selectedEdge = null;
		for(ActivityEdge edge in obj.getOutgoing()) {
			if($[edge].isControlFlow()) {
				ControlFlow controlFlow = $[edge].asControlFlow();
				Value guardValue = controlFlow.getGuard().getCurrentValue();
				if($[guardValue].isBooleanValue()) {
					BooleanValue booleanValue = $[guardValue].asBooleanValue();
					if(booleanValue.isValue()) {
						selectedEdge = edge;
						break;
					}
				}
			}
		}
		
		if(selectedEdge != null) {
			$[obj].addTokens(tokens);
			$[selectedEdge].sendOffer(tokens);
		}
	}
	
	def Boolean isReady() {
		Boolean ready = true;
		for(ActivityEdge edge in obj.getIncoming()) {
			if(not $[edge].hasOffer()) {
				ready = false;
			}
			
		}
		
		return ready;
	}
}
open class ForkNode extends ControlNode {}
open class ForkNode_Aspect extends ControlNode_Aspect {
	def void fire(Sequence(Token) tokens) {
		Sequence(Token) forkedTokens = newSequence(Token);
		for(Token token in tokens) {
			ForkedToken forkedToken = new ForkedToken();
			forkedToken.setBaseToken(token);
			forkedToken.setRemainingOffersCount(obj.getOutgoing().size());
			forkedTokens.add(forkedToken);
		}
		
		$[obj].addTokens(forkedTokens);
		$[obj].sendOffers(forkedTokens);
	}
}

open class InitialNode extends ControlNode {}
open class InitialNode_Aspect extends ControlNode_Aspect {
	def void fire(Sequence(Token) tokens) {
		Sequence(Token) producedTokens = newSequence(Token);
		producedTokens.add(new ControlToken());
		$[obj].addTokens(producedTokens);
		$[obj].sendOffers(producedTokens);
	}
	
	def Boolean isReady() {
		return false;
	}
	
	def Boolean isInitialNode() {
		return true;
	}
	
	def InitialNode asInitialNode() {
		return obj;
	}
}

open class JoinNode extends ControlNode {}
open class JoinNode_Aspect extends ControlNode_Aspect {
	def Boolean isReady() {
		Boolean ready = true;
		for(ActivityEdge edge in obj.getIncoming()) {
			if(not $[edge].hasOffer()) {
				ready = false;
			}
		}
		return ready;
	}
}

open class MergeNode extends ControlNode {}
open class MergeNode_Aspect extends ControlNode_Aspect {
	def Boolean hasOffers() {
		for(ActivityEdge edge in obj.getIncoming()) {
			if($[edge].hasOffer()) {
				return true;
			}
		}
		
		return false;
	}
}


open class OpaqueAction extends Action {}
open class OpaqueAction_Aspect extends Action_Aspect {
	def void execute() {} // abstract
	def void doAction() {
		for(Expression e in obj.getExpressions()) {
			$[e].execute(); 
		}		
	}
}

open class BooleanBinaryExpression extends BooleanExpression {}
open class BooleanBinaryExpression_Aspect extends BooleanExpression_Aspect {
	def void execute() {
		Boolean operandValue1 = $[obj].getCurrentValue(obj.getOperand1());
		Boolean operandValue2 = $[obj].getCurrentValue(obj.getOperand2());
		
		Boolean result = null;
		if(obj.getOperator() == activitydiagram.BooleanBinaryOperator.AND) {
			result = operandValue1 and operandValue2;
		} else {
			result = operandValue1 or operandValue2;
		}
		
		$[obj].assignValue(result);
	}
}

open class BooleanExpression extends Expression {}
open class BooleanExpression_Aspect extends Expression {
	def void assignValue(Boolean value) {
		BooleanValue resultValue = new BooleanValue();
		resultValue.setValue(value);
		BooleanVariable assignee = obj.getAssignee();
		assignee.setCurrentValue(resultValue);
	}
	
	def Boolean getCurrentValue(BooleanVariable variable) {
		Boolean currentValue = null;
		Value value = variable.getCurrentValue();
		if($[value].isBooleanValue()) {
			BooleanValue booleanValue = $[value].asBooleanValue();
			currentValue = booleanValue.isValue();
		}
		
		return currentValue;
	}
}

open class BooleanUnaryExpression extends BooleanExpression {}
open class BooleanUnaryExpression_Aspect extends BooleanExpression_Aspect {
	def void execute() {
		Boolean operandValue = $[obj].getCurrentValue(obj.getOperand());
		$[obj].assignValue(not operandValue);
	}
}

open class IntegerCalculationExpression extends IntegerExpression {}
open class IntegerCalculationExpression_Aspect extends IntegerExpression_Aspect {
	def void execute() {
		Integer operandValue1 = $[obj].getCurrentValue(obj.getOperand1());
		Integer operandValue2 = $[obj].getCurrentValue(obj.getOperand2());

		Integer result = null;

		if(obj.getOperator() == activitydiagram.IntegerCalculationOperator.ADD) {
			result = operandValue1 + operandValue2;
		} else {
			result = operandValue1 - operandValue2;
		}
		IntegerValue resultValue = new IntegerValue();
		resultValue.setValue(result);
		obj.getAssignee().setCurrentValue(resultValue);
		
	}
}
open class IntegerComparisonExpression extends IntegerExpression {}
open class IntegerComparisonExpression_Aspect extends IntegerExpression_Aspect {
	def void execute() {
		Integer operandValue1 = $[obj].getCurrentValue(obj.getOperand1());
		Integer operandValue2 = $[obj].getCurrentValue(obj.getOperand2());

		Boolean result = null;

		if(obj.getOperator() == activitydiagram.IntegerComparisonOperator.EQUALS) {
			result = operandValue1 == operandValue2;
		} else {
			if(obj.getOperator() == activitydiagram.IntegerComparisonOperator.GREATER) {
			result = operandValue1 > operandValue2;
		} else {
			if(obj.getOperator() == activitydiagram.IntegerComparisonOperator.GREATER_EQUALS) {
			result = operandValue1 >= operandValue2;
		} else {
			if(obj.getOperator() == activitydiagram.IntegerComparisonOperator.SMALLER) {
			result = operandValue1 < operandValue2;
		} else {
			result = operandValue1 <= operandValue2;
		}}}}
		BooleanValue resultValue = new BooleanValue();
		resultValue.setValue(result);
		obj.getAssignee().setCurrentValue(resultValue);
	}
}
open class IntegerExpression extends Expression {}
open class IntegerExpression_Aspect extends Expression {
	def Integer getCurrentValue(Variable variable) {
		Integer currentValue = null;
		Value value = variable.getCurrentValue();
		if($[value].isIntegerValue()) {
			IntegerValue integerValue = $[value].asIntegerValue();
			currentValue = integerValue.getValue();
		}
		
		return currentValue;
	}
}


open class ControlFlow extends ActivityEdge {}
open class ControlFlow_Aspect extends ActivityEdge_Aspect {
	def Boolean isControlFlow() {
		return true;
	}
	
	def ControlFlow asControlFlow() {
		return obj;
	}
}
open class IntegerVariable_Aspect {}
open class BooleanVariable_Aspect {}


open class Value {
	def Boolean isBooleanValue() {
		return false;
	}
	
	def BooleanValue asBooleanValue() {
		return null;
	}
	
	def Boolean isIntegerValue() {
		return false;
	}
	
	def IntegerValue asIntegerValue() {
		return null;
	}
}

open class BooleanValue extends Value {
	def Boolean isBooleanValue() {
		return true;
	}
	
	def BooleanValue asBooleanValue() {
		return obj;
	}
}
open class IntegerValue extends Value {
	def Boolean isIntegerValue() {
		return true;		
	}
	
	def IntegerValue asIntegerValue() {
		return obj;
	}
}

open class ExecutableNode extends ActivityNode {}
open class ExecutableNode_Aspect extends ActivityNode_Aspect {
	
}

open class ActivityEdge {
	def void sendOffer(Sequence(Token) tokens) {
		// 	nothing
	}
	
	def Sequence(Token) takeOfferedTokens() {
		return null;
	}
	
	def Boolean hasOffer() {
		return false;
	}
	
	def Boolean isControlFlow() {
		return false;
	}
	
	def ControlFlow asControlFlow() {
		return null;
	}
}

open class ActivityNode {
	def Boolean isReady() {
		return false;
	}
	
	def Sequence(Token) takeOfferedTokens() {
		return null;
	}
	
	def void fire(Sequence(Token) tokens) {}
	def Boolean isInitialNode() {
		return false;
	}
	
	def InitialNode asInitialNode() {
		return null;
	}
	
	def void run() {}
	def void terminate() {}
	
	def void removeToken(Token token) {
	}
}



open class Activity {
	def void terminate() {}
}
